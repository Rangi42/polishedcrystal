FixNewboxChecksum:
	; file pointer must be at species byte
	set #1, #zero
	set #result, 127
.loop1
	increment #1
	readbyte #2
	mulacum #result, #2, #1
	iflt #1, 32, .loop1

	pushpos
	increment #1 ; skip 33 (quirk in game code)
.loop2
	increment #1
	readbyte #2
	and #2, 0x7f
	mulacum #result, #2, #1
	iflt #1, 50, .loop2

	poppos
	rotateleft #result, -15
	set #3, 16
.write_loop
	getfilebyte #1
	shiftleft #2, #result, 7
	and #1, 0x7f
	or #1, #2
	writebyte #1
	rotateleft #result, 1
	decrement #3
	jumpnz #3, .write_loop
	return

AdjustPokemonIndexes:
	set #1, .mon_callback
	call ApplyPartyPatchesOld

	; modify party species
	set #1, 5
	call GetGameDataOffsetConstant
	set #1, #result
	call GetGameDataPointer
	increment #result
	seek #result
.party_species_loop
	call .species_adjust
	getfilebyte #temp
	ifne #temp, 0xff, .party_species_loop

	; modify box species
	set #1, sBox1_v6
	call .box_species

	set #1, sBox8_v6
	call .box_species

	; modify pokedex flags
	seek 0x2a16 ; TODO: replace literal
	set #1, 256
	set #2, #r0
	call ShiftFlags

	seekfwd 32
	set #1, 256
	set #2, #r0
	jump ShiftFlags

; box species adjustments
.box_species
	set #length, 7
	increment #1 ; point to sBoxSpecies
.box_outer_loop
	retz #length
	seek #1
.box_inner_loop
	call .species_adjust
	getfilebyte #temp
	ifne #temp, 0xff, .box_inner_loop
	decrement #length
	add #1, 0x450
	jump .box_outer_loop

.mon_callback
	seek #1
.species_adjust
	getfilebyte #result
	set #address, #r0
.index_loop
	gethalfwordinc #temp, #address ; get offset index upper bound
	ifeq #temp, 0xffff, .done_loop
	gethalfwordinc #offset, #address ; get offset
	ifgt #result, #temp, .index_loop
	add #result, #offset
.done_loop
	writebyte #result
	return
