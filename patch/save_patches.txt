ConvertToNewbox:
	set #a, #zero ; [0, 14)
	set #temp, 28
	seek sBoxMons1UsedEntries
	fillbyte #temp, #zero
	seek sBoxMons2UsedEntries
	fillbyte #temp, #zero

.box_outer_loop
	set #source, sBox1_v6
	iflt #a, 7, .got_source
	set #source, sBox8_v6
.got_source
	remainder #temp, #a, 7
	multiply #temp, BOX_SIZE_v6
	add #source, #temp
	seek #source ; we are now at sBox#Count
	readbyte #b ; box count, we are now at sBox#Species

	set #length, #zero
	set #2, sBox_v6
.copy_temp_loop
	ifge #length, #b, .done_temp_loop
	add #1, #source, 22  ; we are now at sBox#Mon1
	multiply #temp, #length, 32
	add #1, #temp
	set #3, 32 ; size of mon struct
	call CopyFrom1To2
;copy nick
	multiply #temp, #length, 21
	subtract #temp, 828, #temp
	add #1, #temp ; offset from box mon n + 1 to nickname n
	set #3, 7
	call CopyFrom1To2
	increment #1 ; skip terminator byte (will always be 0007+nickname)
	set #3, 3
	call CopyFrom1To2
; copy OT
	subtract #1, 231 ; offset from nickname n+1 to OT n
	set #3, 7
	call CopyFrom1To2
	increment #1 ; skip terminator byte (will always be 0007+OT)
	set #3, 3
	call CopyFrom1To2
	increment #length
	jump .copy_temp_loop

.done_temp_loop
	seek sBoxMons1UsedEntries
	iflt #a, 7, .got_entries
	seek sBoxMons2UsedEntries
.got_entries
	remainder #1, #a, 7
	multiply #1, 20
	add #length, #1, #b
	set #2, 1
.set_flags_loop
	ifge #1, #length, .done_flags_loop
	push #1
	call FlagAction
	pop #1
	increment #1
	jump .set_flags_loop

.done_flags_loop
; copy all of temp back to pokedb
	set #1, sBox_v6
	remainder #temp, #a, 7
	multiply #temp, 1040
	add #2, sBoxMons1, #temp
	iflt #a, 7, .got_target
	add #2, sBoxMons2, #temp
.got_target
	multiply #3, #b, 52
	call CopyFrom1To2
	increment #a
	iflt #a, 14, .box_outer_loop

; we can now overwrite old sBox with newbox
	set #a, #zero
	set #target, sBox1
	set #source, sBoxMons1UsedEntries
	set #length, 140
	call .newbox_outer_loop

	; #a = 7, #target = sBox8
	set #source, sBoxMons2UsedEntries
	set #length, 140
	call .newbox_outer_loop

	seek #target
	fillbyte 23, #zero ; clear out box 15
	writedata .box15, 6
	return

.newbox_outer_loop
	set #length, #zero
.copy_flags_loop
	seek #source
	remainder #1, #a, 7
	multiply #1, 20
	add #1, #length
	set #2, -1 ; we just want to check the flag
	push #1
	call FlagAction
	pop #1
	increment #1
	seek #target
	jumpz #result, .got_value ; write 0 if flag is cleared
	set #result, #1
.got_value
	writebyte #result
	increment #target
	increment #length
	iflt #length, 20, .copy_flags_loop

; set sram bank (0 for box1-7, 1 for box8-14)
	set #temp, #zero
	iflt #a, 7, .sram_bank_2
	decrement #temp
.sram_bank_2
	fillbyte 3, #temp
	add #target, 3 ; target now points at box name

	set #1, BOX_NAMES_OFFSET
	call GetGameDataPointer
	set #3, 9
	multiply #1, #a, #3
	add #1, #result
	set #2, #target
	add #target, #3
	call CopyFrom1To2

	increment #a
	remainder #temp, #a, 7
	jumpnz #temp, .newbox_outer_loop
	return

.box15
	db 0x81, 0xae, 0xb7, 0xe1, 0xe5, 0x53 ; "Box15@"
